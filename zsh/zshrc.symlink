# shortcut to this dotfiles path is $DOTFILES_ZSH
# set this to where your dotfiles are kept
export DOTFILES_ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Coding

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES_ZSH/**/*.zsh)

# load all of the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# init autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load all the completions after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

# tell user the date
$DOTFILES_ZSH/bin/today

unset config_files
